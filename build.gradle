plugins {
  id 'application'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.0.6.RELEASE'
  id 'org.springframework.boot' version '2.1.5.RELEASE'
  id 'org.owasp.dependencycheck' version '3.3.4'
  id 'com.github.ben-manes.versions' version '0.20.0'
  id 'org.sonarqube' version '2.6.2'
  id 'org.unbroken-dome.test-sets' version '2.1.1'
}

group = 'uk.gov.hmcts.reform.cmc'
version = '0.0.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

testSets {
    integrationTest
}

sourceSets {
    functionalTest
    smokeTest
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestCompileOnly.extendsFrom testCompileOnly
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
    
    functionalTestCompile.extendsFrom smokeTestCompile
    functionalTestCompileOnly.extendsFrom smokeTestCompileOnly
    functionalTestImplementation.extendsFrom smokeTestImplementation
    functionalTestRuntimeOnly.extendsFrom smokeTestRuntimeOnly
}

tasks.withType(Test) {
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
}

task functional(type: Test, description: 'Runs the functional tests.', group: 'Verification') {
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
}

task smoke(type: Test, description: 'Runs the smoke Tests.', group: 'Verification') {
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}


checkstyle {
  maxWarnings = 50
  toolVersion = '8.18'
  // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
  configDir = new File(rootDir, 'config/checkstyle')
}

pmd {
  toolVersion = "6.9.0"
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.functionalTest, sourceSets.integrationTest, sourceSets.smokeTest]
  reportsDir = file("$project.buildDir/reports/pmd")
  ruleSetFiles = files("config/pmd/ruleset.xml")
  ignoreFailures = true
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.enabled = true
    csv.enabled = false
    xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

project.tasks['sonarqube'].dependsOn test, integration

sonarqube {
  properties {
    property "sonar.projectName", "CMC :: Claim Submit API"
    property "sonar.projectKey", "uk.gov.hmcts.reform:cmc-claim-submit-api"
    property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
    property "sonar.jacoco.itReportPath", "${project.buildDir}/jacoco/integration.exec"

  }
}

dependencyUpdates.resolutionStrategy = {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'config/owasp/suppressions.xml'
}

repositories {
  mavenLocal()
  jcenter()
  mavenCentral()
}

// it is important to specify logback classic and core packages explicitly as libraries like spring boot
// enforces it's own (older) version which is not recommended.
def versions = [
  junit           : '5.3.2',
  reformLogging   : '6.0.1',
  springBoot      : springBoot.class.package.implementationVersion,
  springfoxSwagger: '2.9.2'
]

dependencies {
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-json'
  compile group: 'uk.gov.hmcts.reform', name: 'health-spring-boot-starter', version: '0.0.3'
  compile group: 'uk.gov.hmcts.reform', name: 'properties-volume-spring-boot-starter', version:'0.0.4'

  compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign'
  compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-netflix-hystrix', version: '2.1.0.RELEASE'
  
  compile group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger
  compile group: 'io.springfox', name: 'springfox-swagger-ui', version: versions.springfoxSwagger

  compile group: 'uk.gov.hmcts.reform', name: 'java-logging', version: versions.reformLogging
  compile group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: '3.0.0'
  compile group: 'uk.gov.hmcts.reform', name: 'core-case-data-store-client', version: '4.6.3'
  
  compile group: 'org.elasticsearch', name: 'elasticsearch', version: '6.4.2'
  compile group: 'io.jsonwebtoken', name: 'jjwt', version: '0.9.1'  
  compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'
  
  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.6'
  compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.6'

  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'

  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
  testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: versions.junit
  testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit
  integrationTestCompile sourceSets.test.runtimeClasspath

  smokeTestCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
  smokeTestCompile group: 'org.springframework.boot', name: 'spring-boot-starter-json'  
  smokeTestCompile group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: '3.0.0'
  smokeTestCompile group: 'uk.gov.hmcts.reform', name: 'idam-client', version: '1.0.2'
  smokeTestCompile group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign'
  smokeTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
  smokeTestCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: versions.junit
  smokeTestRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit
}


mainClassName = 'uk.gov.hmcts.reform.cmc.submit.Application'

bootJar {
  archiveName = 'cmc-claim-submit-api.jar'

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}
